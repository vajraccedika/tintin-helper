#class multidisplay kill
#class multidisplay open

#nop --
#nop Class Definitions
#nop --

#var nukefire/multidisplay-description {Manages multi char info display in HUD}
#var nukefire/multidisplay-help {
	Displays group HMV info in right panel, if right\n
	panel layout is loaded.
}

#nop --
#nop Modloader Stuff
#nop --

#alias nukefire/multidisplay-register {
	#if {@isloaded{modloader}} {
		register_module nukefire/multidisplay
	} {
		fail_module nukefire/multidisplay unknown reason
	}
}



#nop --
#nop Events
#nop --

#event {VARIABLE UPDATED MSDP_GROUP} {event_raise {variable MSDP_GROUP}}
#event {SESSION DEACTIVATED} {event_raise {SESSION DEACTIVATED} {%0}}

#nop event_register {variable MSDP_HEALTH} multidisplay {generate_group_info}
#nop event_register {variable MSDP_MANA} multidisplay {generate_group_info}
#nop event_register {variable MSDP_MOVEMENT} multidisplay {generate_group_info}
#nop event_register prompt multidisplay {generate_group_info}
	
event_register prompt msdp_group {#delay {msdp_group} {generate_msdp_group} {0.1}}

event_register player_death clear_group_info {clear_group_info}
event_register player_death clear_msdp_group {clear_msdp_group}

event_register {SESSION ACTIVATED} amultidisplay {set_active_session %2}
event_register {SESSION DEACTIVATED} multidisplay {set_inactive_session %2}

#nop set right tile for group display
set_right_tile 2 <B85678f> group_info[%*] 10 tile

#nop --
#nop Alias
#nop --

#alias set_active_session {
	#nop %0;
	#nop #all #show {<cfa>Ses "%0" activated};
	#var act_ses[$MSDP_CHARACTER_NAME] 1;
	#if {@is_alias{display_right_tiles} && $act_ses[$MSDP_CHARACTER_NAME] == 1} {display_right_tiles};
	#if {@is_alias{display_left_tiles} && $act_ses[$MSDP_CHARACTER_NAME] == 1} {display_left_tiles}
}

#alias set_inactive_session {
	#nop %0;
	#nop #all #show {<cfa>Ses "%0" deactivated};
	#var act_ses[$MSDP_CHARACTER_NAME] 0
}

#alias generate_group_info {
	#nop %0;
	diaLog info Generate group_info called by %1;
	#if {&{MSDP_CHARACTER_NAME}} {
		#list group_info create {<Ffff>Connected characters:};
		#foreach {*MSDP_GROUP[%*]} {_ginf} {
			#format _disp_line {%-9s - %.3s [%+2s] %+3s<Ffff>H %+3s<Ffff>M %+3s<Ffff>V [%.8s]}
			{@disp_name_color{$MSDP_GROUP[$_ginf][name];$_ginf}}
			{$MSDP_GROUP[$_ginf][class]}
			{$MSDP_GROUP[$_ginf][level]}
			{@colorPct{$MSDP_GROUP[$_ginf][health]}}
			{@colorPct{$MSDP_GROUP[$_ginf][mana]}}
			{@colorPct{$MSDP_GROUP[$_ginf][movement]}}
			{$MSDP_GROUP[$_ginf][position]};
			#list group_info add {{$_disp_line}}
		};
		#if {@is_alias{display_right_tiles} && $act_ses[$MSDP_CHARACTER_NAME] == 1} {display_right_tiles}
	}
}


#alias generate_msdp_group {
	#nop %0;
	diaLog info Generating MSDP_GROUP, called by %1;
	#if {&{MSDP_CHARACTER_NAME}} {
		#var msdp_idx &{MSDP_GROUP[]};
		#math mg[health] @int{@pct{$MSDP_HEALTH;$MSDP_HEALTH_MAX}};
		#math mg[mana] @int{@pct{$MSDP_MANA;$MSDP_MANA_MAX}};
		#math mg[movement] @int{@pct{$MSDP_MOVEMENT;$MSDP_MOVEMENT_MAX}};
		#var mg[position] $MSDP_POSITION;
		#var mg[roomhash] $new_room_hash;
		#var mg[room] $MSDP_ROOM_VNUM;
		#var mg[level] $MSDP_LEVEL;
		#var mg[name] $MSDP_CHARACTER_NAME;
		#var mg_index @getMGindex{$MSDP_CHARACTER_NAME};
		#if {$mg_index > 0} {
			#nop #all #var MSDP_GROUP[$mg_index] {${mg}};
			#foreach {$valid_sessions[%*]} {_s} {
				#$_s #var MSDP_GROUP[$mg_index] {${mg}}
			}
		};
		#else {
			#math new_mg_idx $msdp_idx + 1;
			#nop #all #var MSDP_GROUP[$new_mg_idx] {${mg}};
			#foreach {$valid_sessions[%*]} {_s} {
				#$_s #var MSDP_GROUP[$new_mg_idx] {${mg}}
			}
		}
	}
}

#alias clear_msdp_group {
	#nop %0;
	#var _ses @validSessions{};
	#foreach {$_ses[%*]} {_s} {
		#$_s #unvar MSDP_GROUP[$mg_index] {${mg}}
	}
}

#alias clear_group_info {
	#nop %0;
	#var _ses @validSessions{};
	#foreach {$_ses[%*]} {_s} {
		#${_s} #unvar group_info[$MSDP_CHARACTER_NAME]
	}
}

#alias register_session {
	#var valid_sessions @validSessions{};
	#foreach {$valid_sessions[%*]} {_ses} {
		#${_ses} {#var valid_sessions {${valid_sessions}}};
		#var mg_index @getMGindex{$MSDP_CHARACTER_NAME}
	}
}


#nop --
#nop Functions
#nop --

#function validSessions {
	#info sessions save;
	#list valid_sessions clear;
	#foreach {*info[SESSIONS][%*]} {_ses} {
		#if {"$info[SESSIONS][$_ses][IP]" == "144.126.216.139"} {
			#list valid_sessions add $_ses
		}
	};
	#return $valid_sessions
}

#function getMGindex {
	#foreach {*MSDP_GROUP[%*]} {_mg} {
		#if {"$MSDP_GROUP[$_mg][name]" == "%1"} {
			#return $_mg
		}
	};
	#return 0
}

#function disp_name_color {
	#var _col <288>;
	diaLog info received arg:%1:%2;
	#if {$act_ses[$MSDP_CHARACTER_NAME] == 1 && "%1" == "$MSDP_CHARACTER_NAME"} {
		diaLog info Session is active;
		#var _col <188>
	};
	#elseif {$MSDP_GROUP[%2][roomhash] != $new_room_hash} {
		#var _col <f00c>
	};
	#return {${_col}%1<288>}
}

#function with_me {
	#foreach {*MSDP_GROUP[%*]} {_wm} {
		#if {"$MSDP_GROUP[$_wm][name]" == "%1"} {
			#if {$MSDP_GROUP[$_wm][roomhash] == $new_room_hash} {
				#return 1
			}
		}
	};
	#return 0
}

register_session

#class multidisplay close